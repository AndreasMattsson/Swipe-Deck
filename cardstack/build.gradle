apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = 'com.daprlabs.aaron'
version = '0.3.1-beta0'

android {
    compileSdkVersion 23

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 28
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:23.2.0'
    implementation 'com.android.support:design:23.2.0'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = project.group
            artifactId project.name
            version = project.version
            
            artifact("$buildDir/outputs/aar/${project.name}-release.aar") {
                builtBy assembleRelease
            }
            
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.compile.allDependencies.each {
                    if (it instanceof ExternalModuleDependency) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}